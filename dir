`dir` is used to display a directory listing.

This has three modes of operation:

    dir

When invoked with no arguments, this will display the files
(as returned by `file-list`) in a horizontal format.

    dir matching text

When invoked like this, it will display the file names that
contain the specified string.

    dir not-matching text

When invoked like this, it will display the file names that
do not contain the specified string.

    dir long

When invoked with `long`, this will display a vertical listing.

~~~
:display:long
  file-list [ s:put nl ] a:for-each ;

:match?
  dup #1 get-argument s:contains-string? ;

:display:matching
  file-list [ match? [ s:put sp ] [ drop ] choose ] a:for-each nl ;

:display:not-matching
  file-list [ match? [ drop ] [ s:put sp ] choose ] a:for-each nl ;

[ #0 get-argument
  'long     [ display:long ] s:case
  'matching [ display:matching ] s:case
  'not-matching [ display:not-matching ] s:case
  drop file-list [ s:put sp ] a:for-each nl
] call
~~~
